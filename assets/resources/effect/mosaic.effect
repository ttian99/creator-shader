// Copyright (c) 2017-2018 Xiamen Yaji Software Co., Ltd.  

CCEffect %{
  techniques:
  - passes:
    - vert: vs
      frag: fs
      blendState:
        targets:
        - blend: true
      rasterizerState:
        cullMode: none
      properties:
        texture: { value: white }
        alphaThreshold: { value: 0.5 }
        #x轴方块数量
        xBlockCount: {
          value: 10.0,
          editor: {
            tooltip: "x轴方块数量",
          }
        }
        #y轴方块数量
        yBlockCount: {
          value: 10.0,
          editor: {
            tooltip: "x轴方块数量",
          }
        }
}%


CCProgram vs %{
  precision highp float;

  #include <cc-global>
  #include <cc-local>

  in vec3 a_position;
  in vec4 a_color;
  out vec4 v_color;

  #if USE_TEXTURE
  in vec2 a_uv0;
  out vec2 v_uv0;
  #endif

  void main () {
    vec4 pos = vec4(a_position, 1);

    #if CC_USE_MODEL
    pos = cc_matViewProj * cc_matWorld * pos;
    #else
    pos = cc_matViewProj * pos;
    #endif

    #if USE_TEXTURE
    v_uv0 = a_uv0;
    #endif

    v_color = a_color;

    gl_Position = pos;
  }
}%


CCProgram fs %{
  precision highp float;
  
  #include <alpha-test>
  #include <texture>

  in vec4 v_color;

  #if USE_TEXTURE
  in vec2 v_uv0;
  uniform sampler2D texture;
  #endif

  #if USE_MOSAIC
  uniform Mosaic {
    float xBlockCount;
    float yBlockCount;
  };
  vec2 getUvMapPos(vec2 p) {
    // x轴宽度
    float xBlockWidth = 1.0 / max(xBlockCount, 1.0);
    // 计算点在x轴的哪个格子
    float xBlockIndex = floor(p.x / xBlockWidth);
    // y轴宽度
    float yBlockWidth = 1.0 / max(yBlockCount, 1.0);
    float yBlockIndex = floor(p.y / yBlockWidth);
    // 计算中心点坐标
    vec2 pos = vec2((xBlockIndex + 0.5) * xBlockWidth, (yBlockIndex + 0.5) * yBlockWidth);
    return pos;
  }
  #endif

  void main () {
    vec4 o = vec4(1, 1, 1, 1);

    vec2 realPos = v_uv0;
    #if USE_MOSAIC
      realPos = getUvMapPos(v_uv0);
    #endif
    #if USE_TEXTURE
      CCTexture(texture, realPos, o);
    #endif

    o *= v_color;

    ALPHA_TEST(o);

    gl_FragColor = o;
  }
}%
